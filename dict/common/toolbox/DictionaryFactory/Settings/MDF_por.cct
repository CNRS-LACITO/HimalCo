c ====================================================================
c MDF_POR.CCT - Translate MDF labels into Portuguese
c Uses standard Windows ANSI or Macintosh Roman character set
c Acknowledgment: Translated by Bob Wright (ICTS Software Support)
c For Shoebox version 4.0 and later
c See the *** comments to learn how to modify this change table.
c --------------------------------------------------------------------

c --------------------------------------------------------------------
begin >
    store(Backslash) "\" endstore

    c *** The MDFDict2.cct and MDFDictH.cct change tables add a period
    c at the end of most data fields. But if the field already
    c ended with final punctuation, the additional period is wrong.
    c This table removes inappropriate periods added by MDF after
    c final punctuation in the data, by default after period,
    c question mark, or exclamation mark. Modify the characters
    c listed below within quotes for your language and font:

    store(FinalPunctuation) ".?!" endstore

    use(gOptionFields)

c ====================================================================
group(gOptionFields)  c Shoebox inserts these fields preceding the data

'\_' > store(omit) use(gRestOfOptionField)

nl > ''

'' > use(gMain)

c --------------------------------------------------------------------
group(gRestOfOptionField)

nl fol(Backslash) > endstore use(gOptionFields)

c ====================================================================
group(gMain)

"\CP " > dup         use(gLabel)          c Centered Paragraph

"\ps " > dup         use(gPartOfSpeech)   c Part of speech (English)
"\pn " > "\ps "      use(gPartOfSpeechN)  c Part of speech (national)

"\" >    dup         use(gRestOfField)    c Any other field

c --------------------------------------------------------------------
group(gRestOfField)

nl fol(Backslash) > dup use(gMain)  c End of field

"|fl{" > dup    use(gLabel)

"|ps{" > dup    use(gPartOfSpeech)
"|pn{" > "|ps{" use(gPartOfSpeechN)

c Remove redundant period at the end of:
 "|fs{.}" prec(FinalPunctuation) > ""   c Fields
"}|fs{.}" prec(FinalPunctuation) > "}"  c Subfields

c ====================================================================
group(gPartOfSpeech)

nl fol(Backslash) > dup use(gMain)  c End of \ps field

"}" > dup use(gRestOfField)  c End of |ps{...} subfield

"." prec(FinalPunctuation) > ""  c Remove redundant period

c *** Change rules for national audience \ps part of speech.
c If you don't need different labels for the national audience,
c or if you've included explicit \pn fields throughout your data,
c then you don't need any rules here. If you do, be sure to include
c a rule for every part of speech that you use.

c Note: The following examples have a c [comment] at the beginning
c of the line to make these changes inactive. When you add your own
c change rules, do NOT include that 'c'.

c "adj"           > "ks"                    c adjective
c "n"             > "kb"                    c noun

c ====================================================================
group(gPartOfSpeechN)

nl fol(Backslash) > dup use(gMain)  c End of \pn field

"}" > dup use(gRestOfField)  c End of |pn{...} subfield

"." prec(FinalPunctuation) > ""  c Remove redundant period

c ====================================================================
group(gLabel)

nl fol(Backslash) > dup use(gMain)  c End of \CP field

"}" > dup use(gRestOfField)  c End of |fl{...} label subfield

c 1999-03-29 MRP: Old labels that aren't used in version 4.11 (and later)

"[Regnl:"         > "[Reg:"                 c \dr, \gr, wr
"NatRestrict:"    > "Restriç:"              c \on Only/restrictions (n)
"NatUsage:"       > "Uso:"                  c \un Usage (n)

c *** Change rules for MDF's standard field labels that are output by
c MDFDict2.cct, MDFDictH.cct, MDFList2.cct, and MDFListH.cct.
c By replacing a rule's right-hand-side, you translate that label
c for a national audience or modify it for an English audience.

c --------------------------------------------------------------------
c If you want to translate the \lf data field, add rules here:

c --------------------------------------------------------------------
c If you want to translate the \pdl data field, add rules here:

c --------------------------------------------------------------------
"Ant:"            > "Antôn:"      c \an Antonym > Antônimo
"Read:"           > "Leia:"       c \bb Bibliography > Bibliografia
"From:"           > "De:"         c \bw Borrowed word (loan) > Préstamo
"See:"            > "Veja:"       c \cf Cross reference > Referência
"Etym:"           > "Etim:"       c \et Etymology (proto form) > Etimologia
"Semantics:"      > "Sem:"        c \is Index of semantics
"Lit:"            > "Lit:"        c \lt Literally > Literal
"See main entry:" > "Veja entrada principal:"  c \mn Main entry cross reference
                                  c     Referência a uma entrada principal
"Morph:"          > "Morf:"       c \mr Morphology > Morfologia
"Anth:"           > "Antro:"      c \na Notes (anthropology) > Notas (anthropologia)
"Disc:"           > "Disc:"       c \nd Notes (discourse) > Notas (discurso)
"Gram:"           > "Gram:"       c \ng Notes (grammar) > Notas (gramática)
"Phon:"           > "Fonol:"      c \np Notes (phonology) > Notas (fonologia)
"Ques:"           > "Perguntas:"  c \nq Notes (questions) > Notas (perguntas)
"Socio:"          > "Socio:"      c \ns Notes (sociolinguistics) > Notas (sociolingüística)
"Note:"           > "Nota:"       c \nt Notes (general) > Notas (gerais)
"Restrict:"       > "Restriç:"    c \o* Only/restrictions (*) > Só/restrições
"Prdm:"           > "Parad:"      c \pd Paradigm > Paradigma
"pl:"             > next
"Pl:"             > "Pl:"         c \pl Plural form > Plural
"Reversal:"       > "Inversão:"   c \r* Reversal (*)
"rd:"             > next
"Redup:"          > "Redup:"      c \rd Reduplication form(s) > Formas reduplicadas
"Ref:"            > "Ref:"        c \rf Reference > Referência
"SD:"             > "CampoSem:"   c \sd Semantic domain > Campo semántico
"sg:"             > next
"Sg:"             > "Sg:"         c \sg Singular form > Singular
"Usage:"          > "Uso:"        c \u* Usage (*) > Uso
"Source:"         > "Fonte:"      c \so Source > Fonte
"Syn:"            > "Sinôn:"      c \sy Synonym [o-acute] > Sinônimo
"Thes:"           > "Tesoro:"     c \th Thesaurus > Tesoro
"Variant:"        > "Var:"        c \va Variant forms > Variante
"Word-level:"     > "Word-level:" c \w* Word-level gloss (*)
"1d:"             > "1d:"         c \1d First dual > Primeiro duplo
"1p:"             > "1p:"         c \1p First plural > Primeiro plural
"1i:"             > next
"1pi:"            > "1pi:"        c \1i First plural inclusive > Primeiro plural inclusivo
"1e:"             > next
"1px:"            > "1pex:"       c \1e First plural exclusive > Primeiro plural exclusivo
"1s:"             > "1s:"         c \1s First singular > Primeiro singular
"2d:"             > "2d:"         c \2d Second dual > Segundo duplo
"2p:"             > "2p:"         c \2p Second plural > Segundo plural
"2s:"             > "2s:"         c \2s Second singular > Segundo singular
"3d:"             > "3d:"         c \3d Third dual > Terceiro duplo
"4d:"             > next
"3dn:"            > "3din:"       c \4d Non-animate dual > Duplo inanimado
"3p:"             > "3p:"         c \3p Third plural > Terceiro plural
"4p:"             > next
"3pn:"            > "3pin:"       c \4p Non-animate plural > Plural inanimado
"3s:"             > "3s:"         c \3s Third singular > Terceira singular
"4s:"             > next
"3sn:"            > "3sin:"       c \4s Non-animate singular > Singular inanimado

"(from:" > "(de:"  c Include \lx lexeme with \lc citation form

"see:" > "Veja:"  c Finderlist cross-reference from \se to main entry
 
"Total number of entries:" > "Número total de entradas: " c Label in \CP
